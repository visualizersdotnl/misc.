<!--
	raymarching experiment (GLSL)
	using three.js (plus some add-ons) by MrDoob as stub

	to do:
		- texture load

	(c) visualizers.nl
-->

<html>
	<head>
		<title>visualizers.nl</title>
		<meta charset = "utf-8">
		<meta name = "viewport" content = "width = device-width, user-scalable = no, minimum-scale = 1.0, maximum-scale = 1.0">
		<style>
			canvas 
			{ 
				width: 100%; height: 100% 
			}

			body 
			{
				background: #000000;
				padding: 0;
				margin: 0;
				font-weight: normal;
				overflow: hidden;
			}

			#info 
			{
				position: absolute;
				top: 0px; width: 100%;
				color: #ffffff;
				padding: 5px;
				font-family: Monospace;
				font-size: 13px;
				text-align: center;
				z-index: 1000;
			}
		</style>
	</head>

	<body style = "margin: 0; padding: 0">
		<script type = "text/javascript" src = "3rdparty/three.js"></script>
		<script type = "text/javascript" src = "3rdparty/THREEx.WindowResize.js"></script>
		<script type = "text/javascript" src = "3rdparty/Detector.js"></script>

		<!--
		<script type = "text/javascript" src = "https://raw.github.com/mrdoob/three.js/master/build/three.js"></script>
		-->

		<script id = "vertexShader" type = "x-shader/x-vertex">
			void main() {
				gl_Position = projectionMatrix*modelViewMatrix*vec4(position, 1.0);
			}
		</script>

		<script id = "fragmentShader" type = "x-shader/x-fragment">
			//
			// constants
			//

			uniform vec2  u_resolution;
			uniform float u_time;

			const float kEps = 0.001;
			const float kMaxDistance = 10000.0;

			//
			// matrix operations
			//

			mat3 matRotation(vec3 axis, float theta)
			{ // avoid this in (inner) loops as it's not exactly quick
				theta *= 0.5;
				axis = normalize(axis)*sin(theta);
				vec4 q = vec4(axis.x, axis.y, axis.z, cos(theta));
				
				float xx = q.x*q.x;
				float yy = q.y*q.y;
				float zz = q.z*q.z;
				float xy = q.x*q.y;
				float xz = q.x*q.z;
				float yz = q.y*q.z;
				float xw = q.x*q.w;
				float yw = q.y*q.w;
				float zw = q.z*q.w;
				
				mat3 res;
				res[0] = vec3(1.0 - 2.0*(yy + zz),       2.0*(xy + zw),       2.0*(xz - yw));
				res[1] = vec3(      2.0*(xy - zw), 1.0 - 2.0*(xx + zz),       2.0*(yz + xw));
				res[2] = vec3(      2.0*(xz + yw),       2.0*(yz - xw), 1.0 - 2.0*(xx + yy));
				return res;
			}

			mat3 matTranspose(mat3 m)
			{
				mat3 res;
				res[0] = vec3(m[0].x, m[1].x, m[2].x);
				res[1] = vec3(m[0].y, m[1].y, m[2].y);
				res[2] = vec3(m[0].z, m[1].z, m[2].z);
				return res;
			}

			//
			// distance function
			//

			float sphere(vec3 p, float r)
			{
				return length(p)-r;
			}

			// 
			// scene, normal & march
			//

			float scene(vec3 p)
			{
//				mat3 m = matRotation(vec3(0.0, 0.0, 1.0), 3.14*cos(0.25*u_time));
//				m = matTranspose(m);
//				p = m*p;
				p.z += u_time*0.65;

				p = mod(p, vec3(0.5)) - 0.25;
				return sphere(p, 0.15);
			}

			// 4-tap normal (for closed objects)
			vec3 solidNormal(vec3 p)
			{
				float dist = scene(p);
				vec3 delta = vec3(
					scene(p - vec3(kEps, 0.0, 0.0)), 
					scene(p - vec3(0.0, kEps, 0.0)), 
					scene(p - vec3(0.0, 0.0, kEps)));
				return normalize(dist-delta);
			}

			// 6-tap normal
			vec3 normal(vec3 p)
			{
				vec2 e = vec2(kEps, 0.0);
				return normalize(vec3(
					scene(p+e.xyy) - scene(p-e.xyy),
					scene(p+e.yxy) - scene(p-e.yxy),
					scene(p+e.yyx) - scene(p-e.yyx)));
			}

			float march(vec3 eye, vec3 dir)
			{
				float dist = 0.0;
				for (int i = 0; i < 32; ++i)
				{
					float step = scene(eye + dir*dist);
					if (step < kEps)
					{
						return dist;
					}

					dist += step;
					if (dist > kMaxDistance) break;
				}

				return kMaxDistance;
			}

			//
			// the playground
			//

			void main(void)
			{
				vec2 p = -1.0 + 2.0*gl_FragCoord.xy/u_resolution;
				p.x *= u_resolution.x/u_resolution.y;

				vec3 eye = vec3(0.0, 0.0, -1.0);
				vec3 dir = normalize(vec3(p.x, p.y, 1.0));

				// cheesy camera rotation
				mat3 m = matRotation(vec3(1.0, sin(u_time), 0.4), u_time*0.25);
				dir = normalize(m*dir);

				float dist = march(eye, dir);

				// old-timey radial color cycling as "fog"
				float rad = 1.0-sqrt(dot(p, p));
				float ang = atan(dir.y, dir.x); // rotate along w/camera
				float spin1 = sin(ang*3.0 + 4.0*u_time + cos(u_time + rad*2.0));
				float spin2 = sin(ang*6.0 + 3.0*u_time + sin(u_time + rad*2.5));
				vec3 fogColor = vec3(0.2 + spin1, spin1, spin1 + 0.2*spin2);
				fogColor.xyz += 0.8;
				fogColor *= exp(1.0-rad);

//				vec3 fogColor = vec3(0.0, 0.0, 0.2);

				if (dist < kMaxDistance)
				{
					// basic textbook lighting (diffuse & specular)
					vec3 p = eye + dist*dir;
					vec3 n = solidNormal(p);
					vec3 l = vec3(0.0, 0.0, -1.0);
					vec3 ld = normalize(l-p);
					float diffuse = max(dot(n, ld), 0.0);

					vec3 v = -dir;
					vec3 h = normalize(ld+v);
					float specular = 1.0*pow(max(dot(n, h), 0.0), 30.0);

//					vec4 sceneColor = vec4(n.x, n.y, n.z, 1.0);
//					gl_FragColor = sceneColor;
					
					vec3 chroma = vec3(1.0);
					vec3 sceneColor = chroma*diffuse + chroma*specular;

					// wonky rim (based on diffuse term)
					float rim = diffuse*diffuse;
					rim = clamp((rim-0.4)*4.0, 0.0, 1.0);
					sceneColor.xyz *= rim;

					// composite with linear fog
//					float fog = clamp(dist*0.3, 0.0, 1.0);
//					gl_FragColor.xyz = mix(sceneColor, fogColor, fog);
					gl_FragColor.xyz = mix(sceneColor, fogColor, smoothstep(0.0, 1.0, dist*0.4));
				}
				else
				{
					gl_FragColor.xyz = fogColor;
				}

				gl_FragColor.w = 1.0;
			}
		</script>

		<script>
			if (false == Detector.webgl)
			{
				// WebGL not supported (or so it seems)
				Detector.addGetWebGLMessage();
			}
			else
			{
				// initialize three.js WebGL renderer
				var renderer = new THREE.WebGLRenderer({ precision: "highp", antialias: false });
				renderer.setSize(window.innerWidth, window.innerHeight);
				document.body.appendChild(renderer.domElement);

				// setup scene & camera
				var scene = new THREE.Scene();
				var camera = new THREE.OrthographicCamera(-1, 1, 1, -1, 0.1, 1000);

				// attach resize handler
				THREEx.WindowResize(renderer, camera);

				// setup uniforms
				var myUniforms = { 
					u_resolution: { type: "v2", value: new THREE.Vector2(window.innerWidth, window.innerHeight) },
					u_time: { type: "f", value: 0.0 }
				};

				// build plane with custom shader material
				var geometry = new THREE.PlaneGeometry(2, 2, 1, 1);
				var material = new THREE.ShaderMaterial(
					{
						uniforms: myUniforms,
						attributes: null, 
						vertexShader: document.getElementById('vertexShader').textContent, 
						fragmentShader: document.getElementById('fragmentShader').textContent }
					);
				var plane = new THREE.Mesh(geometry, material);
				scene.add(plane);

				// anything non-zero will do
				camera.position.z = 0.1;

				// create timer
				var timer = new THREE.Clock(true);

				// render loop func.
				function render() {
					// update & render
					myUniforms.u_resolution.value.x = window.innerWidth;
					myUniforms.u_resolution.value.y = window.innerHeight;
					myUniforms.u_time.value = timer.getElapsedTime();
					renderer.render(scene, camera);

					// loop
					requestAnimationFrame(render);
				}

				render();
			}
		</script>
	</body>
</html>
